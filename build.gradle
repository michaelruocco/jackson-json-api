plugins {
    id 'java'
    id 'com.diffplug.gradle.spotless' version '3.25.0'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.10.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
}

apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group 'com.github.michaelruocco'

scmVersion {
    tag {
        prefix = 'jackson-json-api'
    }
}
project.version = scmVersion.version

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.0'

    testCompile 'org.assertj:assertj-core:3.14.0'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:3.1.10'
    testCompile 'com.github.michaelruocco:file-loader:5.0.1'
    testCompile 'net.javacrumbs.json-unit:json-unit-assertj:2.11.0'
    testCompile 'org.slf4j:slf4j-simple:1.7.29'
    testCompile 'org.mockito:mockito-core:3.1.0'
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'BSD License'
            url 'http://www.opensource.org/licenses/bsd-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'mruoc'
            name 'Michael Ruocco'
            email 'michael.ruocco@hotmail.com'
        }
    }

    scm {
        url 'https://github.com/michaelruocco/jackson-json-api'
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId 'jackson-json-api'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Library to help building API endpoints using JSON API with Jackson')
                root.appendNode('name', 'jackson-json-api')
                root.appendNode('url', 'https://github.com/michaelruocco/jackson-json-api')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'jackson-json-api'
        userOrg = 'michaelruocco'
        licenses = ['bsd']
        vcsUrl = 'https://github.com/michaelruocco/jackson-json-api.git'
        version {
            name = project.version
            desc = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(System.getProperty('build.number'))
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}